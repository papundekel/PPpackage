pkgname=libalpm-pp
pkgver=PKGVER
pkgrel=1
pkgdesc="A libalpm fork."
arch=('x86_64')
license=('GPL')
depends=(pacman=$pkgver)
makedepends=('meson')
options=('strip')
validpgpkeys=('6645B0A8C7005E78DB1D7864F99FFE0FEAE999BD'  # Allan McRae <allan@archlinux.org>
              'B8151B117037781095514CA7BBDFFC92306B1121') # Andrew Gregory (pacman) <andrew@archlinux.org>
source=(https://gitlab.archlinux.org/pacman/pacman/-/releases/v$pkgver/downloads/pacman-$pkgver.tar.xz{,.sig}
        alpm.h.patch
        util.c.patch)
sha256sums=('5a60ac6e6bf995ba6140c7d038c34448df1f3daa4ae7141d2cad88eeb5f1f9d9'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "pacman-${pkgver}"
  # we backport way too often in pacman
  # lets at least make it more convenient
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd "pacman-$pkgver"

  meson --prefix=/usr \
        --buildtype=plain \
        -Dscriptlet-shell=/usr/bin/bash \
        -Dldconfig=/usr/bin/ldconfig \
        build

  meson compile -C build
}

package() {
  cd "pacman-$pkgver"

  DESTDIR="$pkgdir/usr/share/libalpm-pp" meson install -C build
}
